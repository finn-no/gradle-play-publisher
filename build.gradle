buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://artifacts.schibsted.io/artifactory/plugins-release'
            credentials {
                username = System.getenv('ARTIFACTORY_USER') ?: ""
                password = System.getenv('ARTIFACTORY_PASS') ?: ""
            }
        }
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'groovy'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

group = GROUP
version = VERSION_NAME

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {

    repositories {
        mavenCentral()
    }

    compile gradleApi()

    compile 'com.android.tools.build:gradle:1.5.0'
    compile('com.google.apis:google-api-services-androidpublisher:v2-rev20-1.21.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-io:commons-io:2.4'

    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

publishing {
    publications {
        jar(MavenPublication) {
            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME
            artifact sourcesJar
            artifact javadocsJar
            from components.java

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL
                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = "${ARTIFACTORY_CONTEXTURL}"
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = System.getenv('ARTIFACTORY_USER') ?: ""
            password = System.getenv('ARTIFACTORY_PASS') ?: ""
            maven = true

            defaults {
                publications 'jar'
            }

            publishArtifacts = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = System.getenv('ARTIFACTORY_USER') ?: ""
            password = System.getenv('ARTIFACTORY_PASS') ?: ""
            maven = true
        }
    }
}

task javadocsJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allGroovy
}
